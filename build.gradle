plugins {
  id "java-gradle-plugin"
  id 'signing'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '1.3.1'
}

group = "com.carrotsearch.gradle.buildinfra"
version = buildInfraLibs.versions.projectVersion.get()

project.description = "Reusable, opinionated gradle build infrastructure"
def websiteAddress = "https://github.com/carrotsearch/gradle-build-infra"
def vcsAddress = "git@github.com:carrotsearch/gradle-build-infra.git"


repositories {
  mavenCentral()
  gradlePluginPortal()
}

tasks.withType(Javadoc).configureEach {
  it.options.addBooleanOption("Xdoclint:none", true)
}

gradlePlugin {
  website = websiteAddress
  vcsUrl = vcsAddress

  plugins {
    BuildInfraPlugin {
      id = 'com.carrotsearch.gradle.buildinfra'
      implementationClass = 'com.carrotsearch.gradle.buildinfra.BuildInfraPlugin'
      displayName = project.description
      description = project.description
      tags.set(['convention', 'built-utility', 'testing', 'randomizedtesting'])
    }
  }
}

dependencies {
  implementation(buildInfraLibs.spotless.plugin) {
    exclude module: "groovy-xml"
  }
  implementation(buildInfraLibs.forbiddenapis.plugin)
  implementation(buildInfraLibs.benmanes.versions.plugin)
  implementation(buildInfraLibs.version.catalog.update.plugin)
  implementation(buildInfraLibs.jackson.databind)
  implementation(buildInfraLibs.carrotsearch.procfork)
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(buildInfraLibs.versions.minJava.get())
  }
}

tasks.register("sourcesJar", Jar, {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
})

tasks.register("javadocJar", Jar, {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
})

tasks.matching { it.name == "publishPluginMavenPublicationToSonatypeRepository" }.configureEach {
  doFirst {
    if (gradle.startParameter.maxWorkerCount != 1) {
      throw new GradleException("Use --worker-count 1 to prevent multiple uploads.")
    }
  }
}

tasks.register("publishToSonatype", {
  description = "Publish plugin to Sonatype"
  group = "Publishing"
  dependsOn "publishPluginMavenPublicationToSonatypeRepository"
})

publishing {
  repositories {
    maven {
      name = 'build'
      url = project.layout.buildDirectory.dir("maven").get().asFile
    }

    maven {
      name = 'sonatype'
      url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"

      credentials {
        if (project.hasProperty('nexusUsername')) {
          username project.nexusUsername
        }
        if (project.hasProperty('nexusPassword')) {
          password project.nexusPassword
        }
      }
    }
  }

  publications {
    withType(MavenPublication) {
      pom {
        inceptionYear = "2025"

        name = project.name
        description = project.description
        url = websiteAddress

        licenses {
          license {
            name = 'Apache 2'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        organization {
          name = "Carrot Search s.c."
          url = "https://www.carrotsearch.com"
        }

        developers {
          developer {
            id = 'dawid.weiss'
            name = 'Dawid Weiss'
            email = 'dawid.weiss@carrotsearch.com'
          }
        }

        scm {
          connection = 'scm:git:' + vcsAddress
          developerConnection = vcsAddress
          url = websiteAddress
        }
      }
    }
  }
}